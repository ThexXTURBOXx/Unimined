plugins {
    id 'java'
    id 'xyz.wagyourtail.unimined' // version '1.0.0'
}

group 'com.example'
version '1.0-SNAPSHOT'

base {
    archivesName = 'UniminedExampleMod'
}

// this is just here so we can test the outputs easier and clean between tests
unimined.useGlobalCache = false

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenLocal()
    mavenCentral()
}

unimined.minecraft {
    version "1.1"
    side("client")

    mappings {
        mcp("legacy", "5.6-1.1")

        stubs(["official", "searge"]) {
            c("ModLoader", "net/minecraft/src/ModLoader")
            c("BaseMod", "net/minecraft/src/BaseMod")
        }

        devNamespace "mcp"
    }

    jarMod {
    }

    minecraftRemapper.config {
        ignoreConflicts(true)
    }

    runs {
        config("client") {
            jvmArgs("-Djma.debug=true")
        }
    }
}

unimined.wagYourMaven("releases")

dependencies {
    jarMod "risugami:modloader:1.1"
}


processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }

    filesMatching("mcmod.info") {
        expand "version": project.version
    }
}