plugins {
    id 'java'
    id 'xyz.wagyourtail.unimined' // version '1.0.0'
}

group 'com.example'
version '1.0-SNAPSHOT'

base {
    archivesName.set('UniminedExampleMod')
}

// this is just here so we can test the outputs easier and clean between tests
unimined.useGlobalCache = false

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    fabric {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

unimined.minecraft {
    version project.minecraft_version

    mappings {
        intermediary()
        mojmap()
    }

    defaultRemapJar = false
}

unimined.minecraft(sourceSets.fabric) {
    version project.minecraft_version

    mappings {
        mojmap()
    }

    fabric {
        loader project.fabric_version
    }
}

configurations {
    mainImplementation
    implementation {
        extendsFrom fabricImplementation
    }
}

sourceSets {
    main {
        compileClasspath += configurations.mainImplementation
        runtimeClasspath += configurations.mainImplementation
    }
}

dependencies {
    // we need this in main where it isn't by default
    mainImplementation "org.spongepowered:mixin:0.8.5-SNAPSHOT"
}

jar {
    enabled = false
}

processFabricResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}