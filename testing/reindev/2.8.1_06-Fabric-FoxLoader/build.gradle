plugins {
    id 'xyz.wagyourtail.unimined' // version '1.3.5'
}

group = 'com.example'
base.archivesName = 'UniminedExampleMod'
version = '1.0.0'

sourceSets {
    client
    server
    foxLoaderClient
    foxLoaderServer
    fabricClient
    fabricServer
}

unimined {
    // this is just here so we can test the outputs easier and clean between tests
    useGlobalCache = false

    reIndev(sourceSets.client) {
        combineWith(sourceSets.main)
        version project.reindev_version
        side 'client'

        defaultRemapJar = false

        runs.all {
            javaVersion = JavaVersion.VERSION_1_8
            enabled = false
        }
    }

    reIndev(sourceSets.server) {
        combineWith(sourceSets.main)
        version project.reindev_version
        side 'server'

        defaultRemapJar = false

        runs.all {
            javaVersion = JavaVersion.VERSION_1_8
            enabled = false
        }
    }

    reIndev(sourceSets.fabricClient) {
        combineWith(sourceSets.client)
        fabric {
            loader project.fabric_loader_version
        }
        runs.config('client') {
            enabled = true
        }
    }

    reIndev(sourceSets.fabricServer) {
        combineWith(sourceSets.server)
        side 'server'
        fabric {
            loader project.fabric_loader_version
        }
        runs.config('server') {
            enabled = true
        }
    }

    reIndev(sourceSets.foxLoaderClient) {
        combineWith(sourceSets.client)

        foxLoader {
            loader()
            modId = "example-mod"
            modName = "Example Mod"
            modVersion = project.version
            modDescription = "An example mod for FoxLoader!"
            clientMod = "com.example.example.mod.ClientExampleMod"
            serverMod = "com.example.example.mod.ServerExampleMod"
        }

        runs.all {
            enabled = false
        }

        runs.config('client') {
            enabled = true
        }
    }

    reIndev(sourceSets.foxLoaderServer) {
        combineWith(sourceSets.server)

        foxLoader {
            loader()
            modId = "example-mod"
            modName = "Example Mod"
            modVersion = project.version
            modDescription = "An example mod for FoxLoader!"
            clientMod = "com.example.example.mod.ClientExampleMod"
            serverMod = "com.example.example.mod.ServerExampleMod"
        }

        runs.config('server') {
            enabled = true
        }
    }
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}
