plugins {
    id 'java'
    id 'xyz.wagyourtail.unimined' // version '[1.4.0,)'
}

group = 'com.example'
base.archivesName = 'UniminedExampleMod'
version = '1.0.0'

sourceSets {
    client {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    server {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

unimined {
    // this is just here so we can test the outputs easier and clean between tests
    useGlobalCache = false

    reIndev {
        version '2.8.1_06'
        side 'client'

        defaultRemapJar = false

        fabric {
            loader '0.16.0'
        }

        runs.config('client') {
            javaVersion = JavaVersion.VERSION_1_8
            enabled = false
        }
        runs.config('server') {
            javaVersion = JavaVersion.VERSION_1_8
            enabled = false
        }
    }

    reIndev(sourceSets.client) {
        combineWith(sourceSets.main)

        runs.config('client') {
            javaVersion = JavaVersion.VERSION_1_8
            enabled = true
        }
    }

    reIndev(sourceSets.server) {
        combineWith(sourceSets.main)
        side 'server'

        runs.config('server') {
            javaVersion = JavaVersion.VERSION_1_8
            enabled = true
        }
    }
}

repositories {
    unimined.legacyFabricMaven()
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}
